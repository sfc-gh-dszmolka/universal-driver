/*
Jenkinsfile template for use in a multibranch pipeline
 */

def getAgentLabel(platform) {
  switch (platform) {
    case 'linux-x86_64':
      return 'small-node-c7'
    case 'linux-aarch64':
      return 'small-node-aarch64'
    case 'macos-x86_64':
      return 'mac_x86'
    case 'macos-aarch64':
      return 'mac_m1'
    case 'windows-x86_64':
      return 'windows-build-vs2022'
    case 'aix-ppc64':
      return 'small-node-c7'
    default:
      error "Unsupported platform: ${platform}"
  }
}

def platform_architecture(platform) {
  return platform.split('-')[1]
}

def platform_os(platform) {
  return platform.split('-')[0]
}

def run_aix_command(command) {
  sshagent (credentials: ['aix-ssh-key']) {
    sh """
      |ssh ${AIX_USER}@${AIX_HOST} 'bash -s' <<EOF
      |  cd ${AIX_REPO_PATH}
      |  ${command}
      |EOF
    """.stripMargin()
  }
}

def setupRust(platform) {
  // Rust should be already installed on AIX
  if (platform_os(platform) == 'aix') {
    return
  }

  if (platform_os(platform) == 'windows') {
    bat '''
      rustc --version || (
        echo Installing Rust...
        curl -L -sSf https://win.rustup.rs/x86_64 -o rustup-init.exe
        rustup-init.exe -y
        del rustup-init.exe
      )
      set PATH=%PATH%;%USERPROFILE%\\.cargo\\bin
      rustc --version
      cargo --version
    '''.stripMargin()
    return
  }

  sh '''
    if command -v rustc >/dev/null 2>&1; then
      rustup update stable
    else
      curl --proto '=https' --tlsv1.2 -sSf https://sh.rustup.rs | sh -s -- -y
      source $HOME/.cargo/env
    fi
    rustc --version
    cargo --version
  '''.stripMargin()
}


def run_cargo_command(platform, command) {
  if (platform_os(platform) == 'aix') {
    run_aix_command("cargo ${command}")
    return
  }

  if (platform_os(platform) == 'windows') {
    bat """
      set PATH=%PATH%;%USERPROFILE%\\.cargo\\bin
      cargo ${command}
    """
    return
  }

  sh """
    source $HOME/.cargo/env
    cargo ${command}
  """
}

def checkout_repo() {
  checkout scm

  if (platform_os(platform) == 'aix') {
    sshagent (credentials: ['aix-ssh-key']) {
      sh """
        ssh ${AIX_USER}@${AIX_HOST} "mkdir -p ${AIX_REPO_PATH}"
        scp -r \$(pwd)/* ${AIX_USER}@${AIX_HOST}:${AIX_REPO_PATH}
      """
    }
  }
}

def cleanup() {
  if (platform_os(platform) == 'aix') {
    run_aix_command("""
      cd /home/jenkins
      rm -rf ${AIX_REPO_PATH}
    """)
  }
}

def uploadMinicorePackage(platform) {
  if (platform_os(platform) == 'aix') {
    run_aix_command("""
      set -x
      export PLATFORM=${platform}
      export COMMIT_SHA=${GIT_COMMIT}
      ./scripts/package_minicore.sh
    """)
    sshagent (credentials: ['aix-ssh-key']) {
      sh """
        source ./scripts/version.sh
        mkdir -p build
        export PLATFORM=${platform}
        export COMMIT_SHA=${GIT_COMMIT}
        scp -r ${AIX_USER}@${AIX_HOST}:${AIX_REPO_PATH}/build/sf_mini_core_\${PLATFORM}_\${VERSION}_SNAPSHOT_\${COMMIT_SHA}.tar.gz build/
        ./scripts/upload_minicore.sh
      """
    }
    return
  }

  if (platform_os(platform) == 'windows') {
    bat """
      set PATH=%PATH%;%USERPROFILE%\\.cargo\\bin
      set PLATFORM=${platform}
      set COMMIT_SHA=${GIT_COMMIT}
      call scripts\\package_minicore.bat
      call scripts\\upload_minicore.bat
    """
    return
  }

  sh """
    source $HOME/.cargo/env
    export PLATFORM=${platform}
    export COMMIT_SHA=${GIT_COMMIT}
    ./scripts/package_minicore.sh
    ./scripts/upload_minicore.sh
  """
}

pipeline {
  //Various labels available: https://snowflakecomputing.atlassian.net/wiki/spaces/EN/pages/2443215263/Agent+labels+in+jenkins
  agent none
  options { timestamps()
            disableResume() //REQUIRED SECTION - We must use this property, else PRs can get in stuck state for any controller restart
            buildDiscarder(logRotator(daysToKeepStr: '5', numToKeepStr: '50')) // REQUIRED SECTION - retention policy must be set
            timeout(time: 1, unit: 'HOURS') // REQUIRED SECTION - timeout must be set
          }
  triggers {
    cron(env.BRANCH_NAME == 'main' ? 'H H/6 * * *' : '')
  }
  stages {
    stage('Build and Test') {
      matrix {
        axes {
          axis {
            name 'PLATFORM'
            values 'linux-x86_64', 'linux-aarch64', 'windows-x86_64', 'macos-x86_64', 'macos-aarch64', 'aix-ppc64'
          }
        }
        agent {
          label getAgentLabel(PLATFORM)
        }
        environment {
          AIX_REPO_PATH = '/home/jenkins/universal-driver/${BRANCH_NAME}-${BUILD_NUMBER}'
          AIX_HOST = '40.143.95.16'
          AIX_USER = 'jenkins'
        }
        stages {
          stage('checkout') {
            steps {
              checkout_repo()
            }
          }
          stage('setup rust') {
            steps {
              setupRust(PLATFORM)
            }
          }
          stage('build') {
            steps {
              run_cargo_command(PLATFORM, 'build --package sf_mini_core')
            }
          }
          stage('test') {
            steps {
              run_cargo_command(PLATFORM, 'test --package sf_mini_core')
            }
          }
          stage('upload minicore package') {
            steps {
              withCredentials([
                usernamePassword(
                  usernameVariable: 'AWS_ACCESS_KEY_ID',
                  passwordVariable: 'AWS_SECRET_ACCESS_KEY',
                  credentialsId: '063fc85b-62a6-4181-9d72-873b43488411'
                )
              ]) {
                uploadMinicorePackage(PLATFORM)
              }
            }
          }
        }
        post {
          always {
            cleanup()
          }
        }
      }
    }
  }
}
